buildscript {
    repositories {
        mavenCentral()
        google()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.3.1'
        classpath 'org.mozilla.rust-android-gradle:plugin:0.9.3'
    }
}

repositories {
    mavenCentral()
    google()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

apply plugin: 'org.mozilla.rust-android-gradle.rust-android'
apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

group = 'ru.vtosters.lite'

cargo {
    profile = 'release'
    module = './rust-src'
    libname = 'native_effects'
    targets = ["arm", "arm64"]
    prebuiltToolchains = true
    pythonCommand = "python3"
}

android {
    namespace 'ru.vtosters.native_effects'
    compileSdk 32

    defaultConfig {
        minSdkVersion 22
        targetSdkVersion 32
        versionCode 1
        versionName "1.0"
    }
    ndkVersion '25.2.9519653'
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            // TODO: set vt org
            url = uri("https://maven.pkg.github.com/Saoqq/native-effects")
            // TODO: try to resolve from ENV first (for CI/manual publish) and only then use hardcoded read-only
            // github moment: https://github.com/orgs/community/discussions/26634
            // TODO: set vt org
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            groupId 'ru.vtosters.lite'
            artifactId 'native_effects'
            version System.getenv("SHORT_SHA")
            artifact("$buildDir/outputs/aar/native-effects-release.aar")
        }
    }
}

publish.dependsOn('assembleRelease')
preBuild.dependsOn('cargoBuild')
publishGprPublicationToGitHubPackagesRepository.dependsOn('assembleRelease')
